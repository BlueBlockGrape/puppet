# The omegaUp services.
class omegaup::services {
  remote_file { '/var/lib/omegaup/omegaup-backend.tar.xz':
    url      => 'https://omegaup-dist.s3.amazonaws.com/omegaup-backend.tar.xz',
    sha1hash => '65c8f398423de9523bf8ec720fa908ad3a02cb58',
    mode     => 644,
    owner    => 'root',
    group    => 'root',
    require  => File['/var/lib/omegaup'],
  }

  exec { 'omegaup-backend':
    command     => '/bin/tar -xf /var/lib/omegaup/omegaup-backend.tar.xz -C /',
    user        => 'root',
    notify      => File[
      '/usr/bin/omegaup-grader',
      '/usr/bin/omegaup-runner',
      '/usr/bin/omegaup-broadcaster'
    ],
    subscribe   => Remote_File['/var/lib/omegaup/omegaup-backend.tar.xz'],
    refreshonly => true,
  }

  file { ['/usr/bin/omegaup-grader', '/usr/bin/omegaup-runner',
          '/usr/bin/omegaup-broadcaster']:
    require => Exec['omegaup-backend'],
  }
}

# vim:expandtab ts=2 sw=2
